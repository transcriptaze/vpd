;; 'Getting started' example VPX script

new module "bodacious" 1U 10H

new guide vertical @10.16mm
new guide v1+12.7mm
new guide right-10.16mm

new guide horizontal @20.32mm
new guide h1+3H
new guide top+10.16mm
new guide bottom-15.24mm

set background "foundation"

new input "frequency" v1,h1 PJ301M
new input "volume" v1,h2 PJ301M
new output "left"  v3,h4-10.16mm PJ301M
new output "right" v3,h4         PJ301M
new parameter "volume" v2,h1 RoundBlackKnob
new parameter "distortion" v2,h2 RoundBlackKnob
new light "ok" centre,middle LED
new light "recording" centre,middle+12.7mm LED
new widget "channels" @40.64mm,20.32mm square

new label "Thing 1" centre,h3 "StyleScript-Regular" 16pt centre,baseline #c000ff,#00e000
new label "FREQ"  (input  "frequency"+0mm,-5.08mm) "RobotoCondensed-Regular" 8pt centre,baseline
new label "VOL"   (input  "volume"   +0mm,-5.08mm) "RobotoCondensed-Regular" 8pt centre,baseline
new label "LEFT"  (output "left"     -5.08mm,+0mm) "RobotoCondensed-Bold"    8pt right,middle #ffffff,#222222
new label "RIGHT" (output "right"    -5.08mm,+0mm) "RobotoCondensed-Bold"    8pt right,middle #ffffff,#222222

decorate (parameter "volume")     with "CircularGraduations" (scale 1.1)
decorate (parameter "distortion") with "CircularGraduations" (scale 1.1)
decorate (output    "left"  -3.54mm,+0.0mm) with Pad (stretch 2,0.9)
decorate (output    "right" -3.54mm,+0.0mm) with Pad (stretch 2,0.9)